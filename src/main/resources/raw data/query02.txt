let
	Source = Pdf.Tables(File.Contents("D:\Data\Raw Data\강동구\감사담당관\2023.10월+업무추진비+공개(감사담당관).pdf"), [Implementation="1.3"]),
	SelectedTable = Table.SelectRows(Source, each [Kind] = "Table"),
	Tables = Table.SelectColumns(SelectedTable, "Data"),
	TableList = Table.ToColumns(Tables),
	CombinedTable = Table.Combine(TableList{0}),
	NullDeletedTable = Table.RemoveMatchingRows(CombinedTable, {[Column1 = ""], [Column1 = null]}, "Column1"),
	PromotedHeaders = Table.PromoteHeaders(NullDeletedTable, [PromoteAllScalars=true]),
	CleanedHeader = Table.TransformColumnNames(PromotedHeaders, Text.Clean),
	ColumnList = Table.ColumnNames(CleanedHeader),
	SpaceErasedColumnList = List.Generate(() => [x = 0, y = Text.Remove(ColumnList{x}, " ")], each [x] < List.Count(ColumnList), each [x = [x] + 1, y = Text.Remove(ColumnList{x}, " ")], each [y] ),
	ColumnFunction = (colText as text) => 
		if Text.Contains(colText, "장소") or Text.Contains(colText, "가맹점") then "장소"
		else if Text.Contains(colText, "목적") or Text.Contains(colText, "내역") then "목적"
		else if Text.Contains(colText, "부서") then "부서" 
		else if Text.Contains(colText, "방법") then "방법"
		else if Text.Contains(colText, "인원") then "인원"
		else if Text.Contains(colText, "액") then "금액"
		else if Text.Contains(colText, "번") then "번호" 
		else if Text.Contains(colText, "일") then "날짜" 
		else if Text.Contains(colText, "시") then "시간"		
		else colText,
	NewColumnList = List.Generate(() => [x = 0, y = ColumnFunction(SpaceErasedColumnList{x})], each [x] < List.Count(SpaceErasedColumnList), each [x = [x] + 1, y = ColumnFunction(SpaceErasedColumnList{x})], each [y]),	
	HeaderPairList = List.Generate(() => [x = 0, y = {ColumnList{x}, NewColumnList{x}}], each [x] < List.Count(NewColumnList), each [x = [x] + 1, y = {ColumnList{x}, NewColumnList{x}}], each [y]),
	UnifiedHeader = Table.RenameColumns(CleanedHeader, HeaderPairList),
	UnitErasedTable = Table.ReplaceValue(UnifiedHeader, "명", "", Replacer.ReplaceText, {"인원"}),
	TransformedColumnTypes = Table.TransformColumnTypes(UnitErasedTable,{{NewColumnList{0}, Int64.Type}, {NewColumnList{1}, type text}, {NewColumnList{2}, type date}, {NewColumnList{3}, type time}, {NewColumnList{4}, type text}, {NewColumnList{5}, type text}, {NewColumnList{6}, Int64.Type}, {NewColumnList{7}, Currency.Type}, {NewColumnList{8}, type text}, {NewColumnList{9}, type text}}),
	DepartmentAdded = Table.AddColumn(TransformedColumnTypes, "부서", each "감사담당관", type text)
in
	DepartmentAdded